buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.11'
        classpath 'com.github.jruby-gradle:jruby-gradle-plugin:1.2.1'
    }
}

apply plugin: 'org.asciidoctor.convert'
apply plugin: 'com.github.jruby-gradle.base'
apply plugin: 'com.github.ben-manes.versions'

dependencies {
    gems 'rubygems:asciidoctor-diagram:1.4.0'
}

asciidoctor {
    // let jruby-gradle download and install gems
    dependsOn jrubyPrepare

    // include the diagram gem
    requires = ['asciidoctor-diagram']
    gemPath = jrubyPrepare.outputDir

    options doctype: 'book'

    // let's convert to PDF while we're at it
    backends = ['html5', 'pdf']

    attributes 'source-highlighter'     : 'coderay',
               'coderay-linenums-mode'  : 'table',
               'author'                 : 'Authors: The dolphin team',
               'revnumber'              : 'PRE-1.0',
               'icons'                  : 'font',
               'linkattrs'              : true,
               'encoding'               : 'utf-8'

    resources {
        from('src/docs/asciidoc') {
             include '**/*.png'
        }
    }
}

